buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
    }
}

group 'io.github.ouyi.jaxws'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

ext.wsimportOutputDir = "./build/generated-sources/wsimport"

idea {
    module {
        generatedSourceDirs += file(wsimportOutputDir)
    }
}

task wsimport(type:Exec) {
    doFirst {
        mkdir wsimportOutputDir
    }
    commandLine "wsimport", "-keep", "-s", wsimportOutputDir, "-encoding", "UTF-8", "-Xnocompile", "-B-XautoNameResolution", "-p", "io.github.ouyi.jaxws", "src/main/resources/HelloService.wsdl"
}

compileJava {
    dependsOn wsimport
    source wsimportOutputDir
}

